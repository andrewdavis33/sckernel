{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Toy Piece"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Start the audio server."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "ServerOptions.devices"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "s.options.outDevice_(\"Out_Soundflower_Built-In\"); // An aggregate device to capture audio during screen recording"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "s.boot;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "s.meter;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "FreqScope.new"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Instruments"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Tone generated by sending an impulse through resonant filters."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "SynthDef('tone', {\n",
    "    arg outBus = 0, freq = 521, amp = 0.2, pos = 0, atk = 0.05;\n",
    "    var sig;\n",
    "\n",
    "    sig = Klank.ar(`[\n",
    "        [freq, freq*1.501, freq*1.97], // freqs\n",
    "        [0.7, 0.45, 0.25], // amps\n",
    "        [0.2, 0.3, 0.48] // phases\n",
    "    ], Impulse.ar(0));\n",
    "    DetectSilence.ar(sig, doneAction: 2);\n",
    "    sig = sig * EnvGen.kr(Env([0, 1], [atk]));\n",
    "    sig = Pan2.ar(sig, pos, amp);\n",
    "\n",
    "    Out.ar(outBus, sig);\n",
    "}).add;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "String like sound generated by using a variable triangle/saw wave."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "SynthDef('bleep', {\n",
    "    arg outBus = 0, freq = 440, amp = 0.2, len = 1, pos = 0;\n",
    "    var sig, env;\n",
    "\n",
    "    env = EnvGen.kr(\n",
    "        Env([0, amp, 0], [len * (2/3), len/3], curve: 'cubed'),\n",
    "        doneAction: 2\n",
    "    );\n",
    "\n",
    "    sig = VarSaw.ar(\n",
    "        freq: freq,\n",
    "        iphase: 0,\n",
    "        width: env,\n",
    "        mul: 1\n",
    "    );\n",
    "    sig = sig * env;\n",
    "    sig = Pan2.ar(sig, pos);\n",
    "\n",
    "    Out.ar(outBus, sig);\n",
    "}).add;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Reverbs"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A reverberator created through a chain of series allpass filters with dampening from a lowpass filter.  `mix` parameter controls the amount of reverb."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "SynthDef('reverb', {\n",
    "    arg outBus = 0, inBus, mix = 0.4;\n",
    "    var dry, wet, sig, numFilters = 8;\n",
    "\n",
    "    dry = In.ar(inBus, 2);\n",
    "\n",
    "    wet = dry;\n",
    "    numFilters.do({\n",
    "        wet = AllpassN.ar(\n",
    "            in: wet,\n",
    "            maxdelaytime: 0.2,\n",
    "            delaytime: {Rand(0.05, 0.15)} ! 2,\n",
    "            decaytime: {Rand(1, 2)} ! 2\n",
    "        );\n",
    "    });\n",
    "\n",
    "    wet = LPF.ar(wet, 3500);\n",
    "    sig = dry.blend(wet, mix);\n",
    "\n",
    "    Out.ar(outBus, sig);\n",
    "}).add;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "A very wet, washy reverb intended to create background drone."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "SynthDef('wash', {\n",
    "    arg outBus = 0, inBus;\n",
    "    var sig, numFilters = 16;\n",
    "\n",
    "    sig = In.ar(inBus, 2);\n",
    "\n",
    "    numFilters.do({\n",
    "        sig = AllpassN.ar(\n",
    "            in: sig,\n",
    "            maxdelaytime: 0.2,\n",
    "            delaytime: {Rand(0.05, 0.15)} ! 2,\n",
    "            decaytime: {Rand(2, 4)} ! 2\n",
    "        );\n",
    "    });\n",
    "\n",
    "    sig = LPF.ar(sig, 3500);\n",
    "\n",
    "    Out.ar(outBus, sig);\n",
    "}).add;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Setup"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Establish a tempo and instantiate both reverbs."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "~tempo = 60;\n",
    "~clock = TempoClock(~tempo/60);\n",
    "~washBus = Bus.audio(s, 2);\n",
    "~washSynth = Synth('wash', [\\inBus, ~washBus]);\n",
    "~reverbBus = Bus.audio(s, 2);\n",
    "~reverbSynth = Synth('reverb', [\\inBus, ~reverbBus]);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Workspace"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "Pbindef('tonePattern',\n",
    "    'instrument', 'tone',\n",
    "    'scale', #[0, 2, 3, 5, 7, 8, 10],\n",
    "    'degree', Prand([0, 1, 2, 3, 4, 5, 6], inf),\n",
    "    'amp', Pwhite(0.2, 0.3),\n",
    "    'dur', Pwhite(0.1, 0.2),\n",
    "    'octave', 6,\n",
    "    'atk', 0.004,\n",
    "    'outBus', ~washBus\n",
    ").play(~clock);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 28,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "Pbindef('tonePattern').stop;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "Pbindef('string',\n",
    "    'instrument', 'bleep',\n",
    "    'scale', #[0, 2, 3, 5, 7, 8, 10],\n",
    "    'degree', Prand([0, 2, 3], inf),\n",
    "    'amp', 0.2,\n",
    "    'octave', 4,\n",
    "    'len', Pwhite(1, 3),\n",
    "    'dur', 3,\n",
    "    'pos', Pwhite(-0.5, 0.5),\n",
    "    'outBus', ~washBus\n",
    ").play(~clock);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "Pbindef('string').stop;"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Cleanup"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 29,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": []
    }
   ],
   "source": [
    "Pbindef.clear;"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SC_Kernel",
   "language": "text",
   "name": "sckernel"
  },
  "language_info": {
   "codemirror_mode": "sc",
   "mimetype": "sc",
   "name": "sclang"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
